# Copyright (c) 2009 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import os
import sys

SOURCES=['chromeos_cryptohome.cc',
         'chromeos_ime.cc',
         'chromeos_language.cc',
         'chromeos_login.cc',
         'chromeos_mount.cc',
         'chromeos_network.cc',
         'chromeos_power.cc',
         'chromeos_synaptics.cc',
         'chromeos_update.cc',
         'marshal.cc',
         'version_check.cc',
]

env = Environment(
    CPPPATH=['.', '..'],
    CCFLAGS=['-g'],
    LINKFLAGS=['-fPIC'],
    LIBS = ['base', 'chromeos', 'rt', 'synaptics'],
)
for key in Split('CC CXX AR RANLIB LD NM CFLAGS CCFLAGS LINKFLAGS'):
  value = os.environ.get(key)
  if value != None:
    env[key] = Split(value)
env['CCFLAGS'] += ['-fPIC', '-fno-exceptions', '-Wall', '-Werror']

# Fix issue with scons not passing some vars through the environment.
for key in Split('PKG_CONFIG PKG_CONFIG_LIBDIR PKG_CONFIG_PATH SYSROOT'):
  if os.environ.has_key(key):
    env['ENV'][key] = os.environ[key]

# glib, dbus, and ibus environment
env.ParseConfig('%s --cflags --libs dbus-1 glib-2.0 gudev-1.0 dbus-glib-1 ibus-1.0 libpcrecpp' % env['ENV']['PKG_CONFIG'])

Default(env.SharedLibrary('cros', SOURCES))

env_test = Environment (
    CPPPATH=[ '.', '..'],
    CCFLAGS=['-g'],
    LIBS = ['base', 'dl', 'rt'],
)
for key in Split('CC CXX AR RANLIB LD NM CFLAGS CCFLAGS LINKFLAGS'):
  value = os.environ.get(key)
  if value != None:
    env_test[key] = Split(value)
env_test['CCFLAGS'] += ['-fPIC', '-fno-exceptions', '-Wall', '-Werror']

# Fix issue with scons not passing some vars through the environment.
for key in Split('PKG_CONFIG PKG_CONFIG_LIBDIR PKG_CONFIG_PATH SYSROOT'):
  if os.environ.has_key(key):
    env_test['ENV'][key] = os.environ[key]

env_test.ParseConfig('%s --cflags --libs gobject-2.0 dbus-1' % env_test['ENV']['PKG_CONFIG'])

env_gtest = env_test.Clone()
env_gtest.Append(LIBS=['gtest'])

Load = env_test.Object('load.cc')

tests = [
  env_test.Program('monitor_power', ['monitor_power.cc', Load]),
  env_gtest.Program('monitor_language', ['monitor_language.cc', Load]),
  env_test.Program('monitor_network', ['monitor_network.cc', Load]),
  env_test.Program('monitor_mount', ['monitor_mount.cc', Load]),
  env_test.Program('monitor_ime', ['monitor_ime.cc', Load]),
  env_test.Program('login_drive', ['drive_login.cc', Load]),
  env_test.Program('cryptohome_drive', ['drive_cryptohome.cc', Load])
]
test_alias = Alias('test', tests)
AlwaysBuild(test_alias)


